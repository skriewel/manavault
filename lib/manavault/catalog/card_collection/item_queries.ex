defmodule Manavault.Catalog.CardCollection.ItemQueries do
  @moduledoc false

  import Ecto.Query

  alias Manavault.Catalog.CardCollection.ItemQueries.{Base, ValueSummary}
  alias Manavault.Catalog.CollectionItem
  alias Manavault.Repo

  import Manavault.Catalog.PriceFragments,
    only: [price_value_fragment: 2, price_cents_fragment: 2, value_gain_cents_fragment: 2]

  @default_sort %{field: "name", direction: "asc"}

  defdelegate value_summary(filters \\ []), to: ValueSummary
  defdelegate value_dashboard(), to: ValueSummary
  defdelegate location_summaries(), to: ValueSummary

  def list_items(filters \\ [], opts \\ []) when is_list(filters) do
    limit = Keyword.get(opts, :limit, 100)
    offset = Keyword.get(opts, :offset, 0)
    sort = Keyword.get(opts, :sort, @default_sort)

    # Sort and page over item ids only, then load the full rows for that
    # page. Sorting the joined rows directly drags every printing row (~2 KB
    # of image/price JSON) through SQLite's sorter for the whole collection.
    page_ids =
      filters
      |> Base.base_query()
      |> select([item, _printing, _card, _location], item.id)
      |> apply_sort(sort)
      |> limit(^limit)
      |> offset(^offset)

    [include_list_locations: true]
    |> items_query()
    |> where([item, _printing, _card, _location], item.id in subquery(page_ids))
    |> apply_sort(sort)
    |> Repo.all()
  end

  def stream_items(filters \\ [], opts \\ []) when is_list(filters) do
    filters
    |> Base.base_query()
    |> apply_sort(@default_sort)
    |> select([item, printing, card, location], {item, printing, card, location})
    |> Repo.stream(opts)
    |> Stream.map(fn {item, printing, card, location} ->
      %{item | printing: %{printing | card: card}, location_assoc: location}
    end)
  end

  def list_item_groups(filters \\ [], opts \\ []) when is_list(filters) do
    limit = Keyword.get(opts, :limit, 100)
    offset = Keyword.get(opts, :offset, 0)
    sort = Keyword.get(opts, :sort, @default_sort)

    printing_ids =
      filters
      |> Base.base_query()
      |> group_by([item, _printing, _card, _location], item.scryfall_id)
      |> select([item, _printing, _card, _location], item.scryfall_id)
      |> apply_group_sort(sort)
      |> limit(^limit)
      |> offset(^offset)
      |> Repo.all()

    items_by_printing =
      filters
      |> Keyword.delete(:for_trade)
      |> items_query()
      |> where([item, _printing, _card, _location], item.scryfall_id in ^printing_ids)
      |> order_by([item, _printing, _card, _location], asc: item.id)
      |> Repo.all()
      |> Enum.group_by(& &1.scryfall_id)

    Enum.map(printing_ids, fn printing_id ->
      items = Map.fetch!(items_by_printing, printing_id)

      %{
        printing_id: printing_id,
        quantity: Enum.reduce(items, 0, &((&1.quantity || 0) + &2)),
        items: items
      }
    end)
  end

  # All three collection totals for a filter set in one pass over the joined
  # rows. A search requests every one of them (the collection header wants the
  # quantity and entry totals, the groups page wants the group total), and
  # each is a full scan of the same filtered join.
  #
  #   * quantity: summed item quantities (for_trade_quantity under the
  #     for_trade filter)
  #   * entries: collection item rows. Pagination must use this: quantity sums
  #     overshoot the row count, which keeps hasNextPage true past the last
  #     row and pages forever.
  #   * groups: distinct printings
  def item_totals(filters \\ [])

  def item_totals([]) do
    CollectionItem
    |> join(:left, [item], location in assoc(item, :location_assoc))
    |> where([_item, location], is_nil(location.id) or location.kind != "list")
    |> select([item, _location], %{
      quantity: coalesce(sum(item.quantity), 0),
      entries: count(item.id),
      groups: count(item.scryfall_id, :distinct)
    })
    |> Repo.one()
  end

  def item_totals(filters) when is_list(filters) do
    quantity_field =
      if Keyword.get(filters, :for_trade, false), do: :for_trade_quantity, else: :quantity

    filters
    |> Base.base_query()
    |> select([item, _printing, _card, _location], %{
      quantity: coalesce(sum(field(item, ^quantity_field)), 0),
      entries: count(item.id),
      groups: count(item.scryfall_id, :distinct)
    })
    |> Repo.one()
  end

  def count_items(filters \\ []) when is_list(filters), do: item_totals(filters).quantity
  def count_item_entries(filters \\ []) when is_list(filters), do: item_totals(filters).entries
  def count_item_groups(filters \\ []) when is_list(filters), do: item_totals(filters).groups

  def list_item_ids(filters \\ []) when is_list(filters) do
    filters
    |> Base.base_query()
    |> select([item, _printing, _card, _location], item.id)
    |> Repo.all()
  end

  def list_items_by_location(location_id, filters \\ [], opts \\ [])
      when is_list(filters) do
    filters
    |> Keyword.put(:location_id, to_string(location_id))
    |> list_items(opts)
  end

  defp items_query(filters) do
    filters
    |> Base.base_query()
    |> preload([_item, printing, card, location],
      printing: {printing, card: card},
      location_assoc: location
    )
  end

  defp apply_group_sort(query, sort) do
    %{field: field, direction: direction} = normalize_sort(sort)

    case {field, direction} do
      {"quantity", "desc"} ->
        order_by(query, [item, _printing, card, _location],
          desc: sum(item.quantity),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"quantity", _direction} ->
        order_by(query, [item, _printing, card, _location],
          asc: sum(item.quantity),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"set", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: printing.set_name,
          desc: printing.set_code,
          asc: card.name,
          asc: printing.collector_number,
          asc: item.scryfall_id
        )

      {"set", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: printing.set_name,
          asc: printing.set_code,
          asc: card.name,
          asc: printing.collector_number,
          asc: item.scryfall_id
        )

      {"rarity", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc:
            fragment(
              "CASE ? WHEN 'common' THEN 1 WHEN 'uncommon' THEN 2 WHEN 'rare' THEN 3 WHEN 'mythic' THEN 4 ELSE 0 END",
              printing.rarity
            ),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"rarity", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc:
            fragment(
              "CASE ? WHEN 'common' THEN 1 WHEN 'uncommon' THEN 2 WHEN 'rare' THEN 3 WHEN 'mythic' THEN 4 ELSE 0 END",
              printing.rarity
            ),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"price", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: max(price_value_fragment(item, printing)),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"price", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: max(price_value_fragment(item, printing)),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"value_gain", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: sum(item.quantity * value_gain_cents_fragment(item, printing)),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"value_gain", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: sum(item.quantity * value_gain_cents_fragment(item, printing)),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"added", "desc"} ->
        order_by(query, [item, _printing, card, _location],
          desc: max(item.inserted_at),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"added", _direction} ->
        order_by(query, [item, _printing, card, _location],
          asc: min(item.inserted_at),
          asc: card.name,
          asc: item.scryfall_id
        )

      {"name", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.scryfall_id
        )

      {_field, _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.scryfall_id
        )
    end
  end

  defp apply_sort(query, sort) do
    %{field: field, direction: direction} = normalize_sort(sort)

    case {field, direction} do
      {"quantity", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: item.quantity,
          asc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.id
        )

      {"quantity", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: item.quantity,
          asc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.id
        )

      {"set", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: printing.set_name,
          desc: printing.set_code,
          asc: card.name,
          asc: printing.collector_number,
          asc: item.id
        )

      {"set", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: printing.set_name,
          asc: printing.set_code,
          asc: card.name,
          asc: printing.collector_number,
          asc: item.id
        )

      {"rarity", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc:
            fragment(
              "CASE ? WHEN 'common' THEN 1 WHEN 'uncommon' THEN 2 WHEN 'rare' THEN 3 WHEN 'mythic' THEN 4 ELSE 0 END",
              printing.rarity
            ),
          asc: card.name,
          asc: item.id
        )

      {"rarity", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc:
            fragment(
              "CASE ? WHEN 'common' THEN 1 WHEN 'uncommon' THEN 2 WHEN 'rare' THEN 3 WHEN 'mythic' THEN 4 ELSE 0 END",
              printing.rarity
            ),
          asc: card.name,
          asc: item.id
        )

      {"price", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: price_value_fragment(item, printing),
          asc: card.name,
          asc: item.id
        )

      {"price", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: price_value_fragment(item, printing),
          asc: card.name,
          asc: item.id
        )

      {"value_gain", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: value_gain_cents_fragment(item, printing),
          asc: card.name,
          asc: item.id
        )

      {"value_gain", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: value_gain_cents_fragment(item, printing),
          asc: card.name,
          asc: item.id
        )

      {"added", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: item.inserted_at,
          asc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.id
        )

      {"added", _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: item.inserted_at,
          asc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.id
        )

      {"name", "desc"} ->
        order_by(query, [item, printing, card, _location],
          desc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.id
        )

      {_field, _direction} ->
        order_by(query, [item, printing, card, _location],
          asc: card.name,
          asc: printing.set_code,
          asc: printing.collector_number,
          asc: item.id
        )
    end
  end

  defp normalize_sort(sort) when is_map(sort) do
    %{
      field: sort |> Map.get(:field, Map.get(sort, "field")) |> normalize_sort_field(),
      direction:
        sort |> Map.get(:direction, Map.get(sort, "direction")) |> normalize_sort_direction()
    }
  end

  defp normalize_sort(sort) when is_list(sort), do: sort |> Enum.into(%{}) |> normalize_sort()
  defp normalize_sort(_sort), do: @default_sort

  defp normalize_sort_field(value) do
    value = value |> to_string() |> String.trim() |> String.downcase()

    if value in ["quantity", "name", "set", "rarity", "price", "value_gain", "added"] do
      value
    else
      @default_sort.field
    end
  end

  defp normalize_sort_direction(value) do
    value = value |> to_string() |> String.trim() |> String.downcase()

    if value in ["asc", "desc"] do
      value
    else
      @default_sort.direction
    end
  end
end
