defmodule Manavault.Catalog.Printing do
  use Ecto.Schema

  import Ecto.Changeset

  alias Manavault.Catalog.Search.NameMatch

  @primary_key {:scryfall_id, :string, []}
  @foreign_key_type :string
  schema "scryfall_printings" do
    field :set_code, :string
    field :set_name, :string
    field :collector_number, :string
    field :lang, :string
    field :flavor_name, :string
    field :normalized_flavor_name, :string
    field :flavor_text, :string
    field :rarity, :string
    field :finishes, :string, default: "[]"
    field :promo_types, :string, default: "[]"
    field :image_uris, :string, default: "{}"
    field :prices, :string, default: "{}"
    field :released_at, :date
    field :cardmarket_id, :integer
    field :owned_count, :integer, virtual: true, default: 0

    belongs_to :card, Manavault.Catalog.Card,
      references: :oracle_id,
      foreign_key: :oracle_id,
      define_field: true

    has_many :collection_items, Manavault.Catalog.CollectionItem,
      foreign_key: :scryfall_id,
      references: :scryfall_id

    timestamps(type: :utc_datetime)
  end

  def changeset(printing, attrs) do
    printing
    |> cast(attrs, [
      :scryfall_id,
      :oracle_id,
      :set_code,
      :set_name,
      :collector_number,
      :lang,
      :flavor_name,
      :flavor_text,
      :rarity,
      :finishes,
      :promo_types,
      :image_uris,
      :prices,
      :released_at,
      :cardmarket_id
    ])
    |> put_normalized_flavor_name()
    |> validate_required([:scryfall_id, :oracle_id, :set_code, :collector_number, :lang])
  end

  defp put_normalized_flavor_name(changeset) do
    case get_field(changeset, :flavor_name) do
      name when is_binary(name) ->
        put_change(changeset, :normalized_flavor_name, NameMatch.sql_normalize(name))

      _name ->
        put_change(changeset, :normalized_flavor_name, nil)
    end
  end
end
