defmodule ManavaultWeb.Schema.Catalog.QueryResolvers do
  @moduledoc false

  alias Manavault.AI
  alias Manavault.Catalog
  alias ManavaultWeb.Schema.Catalog.{CollectionFields, Errors}
  alias ManavaultWeb.Schema.RelayHelpers

  def home_summary(_parent, _args, _resolution) do
    {:ok,
     %{
       collection_count: Catalog.count_collection_items(),
       location_count: Catalog.count_locations(),
       deck_count: Catalog.count_non_archived_decks()
     }}
  end

  def cards(_parent, args, _resolution) do
    with {:ok, offset, limit} <- RelayHelpers.offset_and_limit(args, 24) do
      cards =
        args
        |> Map.get(:q, "")
        |> Catalog.search_cards(
          limit: limit + 1,
          offset: offset,
          sort: Map.get(args, :sort, %{})
        )

      cards
      |> Enum.take(limit)
      |> RelayHelpers.connection_from_slice(offset, limit, offset + length(cards))
    end
  end

  def card_name_suggestions(_parent, args, _resolution) do
    {:ok, Catalog.suggest_card_names(Map.get(args, :q, ""), limit: Map.get(args, :limit, 5))}
  end

  def set_suggestions(_parent, args, _resolution) do
    {:ok, Catalog.search_sets(Map.get(args, :q, ""), limit: Map.get(args, :limit, 8))}
  end

  def card(_parent, %{id: id}, resolution) do
    with {:ok, id} <- card_id(id, resolution) do
      {:ok, Catalog.get_card_with_printings(id)}
    end
  end

  def card_edhrec(_parent, %{name: name}, _resolution), do: Catalog.card_edhrec(name)

  def reload_scryfall_catalog(_parent, _args, _resolution) do
    case Catalog.reload_scryfall_catalog_async() do
      {:ok, _job} ->
        {:ok,
         %{
           status: "queued",
           message: "Scryfall catalog reload queued."
         }}

      {:error, _changeset} ->
        {:error, "Scryfall catalog reload could not be queued."}
    end
  end

  def reload_scryfall_assets(_parent, _args, _resolution) do
    case Catalog.reload_scryfall_assets_async() do
      {:ok, _job} ->
        {:ok,
         %{
           status: "queued",
           message: "Scryfall symbol and set icon reload queued."
         }}

      {:error, _changeset} ->
        {:error, "Scryfall asset reload could not be queued."}
    end
  end

  def collection_items(_parent, args, resolution) do
    # Paginate on the row count: count_collection_items sums quantities, which
    # overshoots the rows and keeps hasNextPage true past the last real page.
    with {:ok, filters} <- collection_filters(args, resolution),
         total_count <- Catalog.count_collection_item_entries(filters),
         {:ok, offset, limit} <- RelayHelpers.slice_window(args, total_count, 100) do
      opts = [limit: limit, offset: offset, sort: Map.get(args, :sort, %{})]

      filters
      |> Catalog.list_collection_items(opts)
      |> RelayHelpers.connection_from_slice(offset, limit, total_count)
    end
  end

  def collection_item_groups(_parent, args, resolution) do
    with {:ok, filters} <- collection_filters(args, resolution),
         total_count <- Catalog.count_collection_item_groups(filters),
         {:ok, offset, limit} <- RelayHelpers.slice_window(args, total_count, 100) do
      opts = [limit: limit, offset: offset, sort: Map.get(args, :sort, %{})]

      filters
      |> Catalog.list_collection_item_groups(opts)
      |> RelayHelpers.connection_from_slice(offset, limit, total_count)
    end
  end

  def collection_item_count(_parent, args, resolution) do
    with {:ok, filters} <- collection_filters(args, resolution) do
      {:ok, Catalog.count_collection_items(filters)}
    end
  end

  def collection_item_entry_count(_parent, args, resolution) do
    with {:ok, filters} <- collection_filters(args, resolution) do
      {:ok, Catalog.count_collection_item_entries(filters)}
    end
  end

  def collection_value_summary(_parent, args, resolution) do
    with {:ok, filters} <- collection_filters(args, resolution) do
      {:ok,
       filters
       |> Catalog.collection_value_summary()
       |> CollectionFields.collection_value_summary_data()}
    end
  end

  def collection_value_dashboard(_parent, _args, _resolution) do
    {:ok,
     Catalog.collection_value_dashboard()
     |> CollectionFields.collection_value_dashboard_data()}
  end

  def collection_export_csv(_parent, args, resolution) do
    with {:ok, filters} <- collection_filters(args, resolution) do
      Catalog.export_collection_csv(filters)
    end
  end

  def collection_export_text(_parent, args, resolution) do
    with {:ok, filters} <- collection_filters(args, resolution) do
      Catalog.export_collection_text(filters)
    end
  end

  def locations(_parent, args, _resolution) do
    summaries = Catalog.location_summaries()

    locations = Catalog.list_location_summaries(summaries) ++ [unfiled_location(summaries)]

    RelayHelpers.connection_from_list(locations, args)
  end

  def collection_auto_sort_rules(_parent, _args, _resolution) do
    {:ok, Catalog.list_collection_auto_sort_rules()}
  end

  def default_deck_tags(_parent, _args, _resolution) do
    {:ok, Catalog.list_default_deck_tags()}
  end

  def location(_parent, %{id: id}, resolution) do
    with {:ok, id} <- RelayHelpers.node_id(id, :location, resolution) do
      location_by_id(id)
    end
  end

  def decks(_parent, args, _resolution) do
    total_count = Catalog.count_decks()

    with {:ok, offset, limit} <- RelayHelpers.slice_window(args, total_count) do
      Catalog.list_deck_summaries(limit: limit, offset: offset)
      |> RelayHelpers.connection_from_slice(offset, limit, total_count)
    end
  end

  def random_deck(_parent, args, resolution) do
    with {:ok, exclude_id} <-
           RelayHelpers.optional_node_id(Map.get(args, :exclude_id), :deck, resolution) do
      {:ok, Catalog.random_deck(exclude_id: exclude_id)}
    end
  end

  def deck(_parent, %{id: id}, resolution) do
    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      {:ok, Catalog.get_deck!(id, preload?: false)}
    end
  end

  def deck_analysis_requests(_parent, %{limit: limit}, _resolution) do
    {:ok, AI.list_deck_analysis_requests(limit: limit)}
  end

  def deck_question_answers(_parent, %{deck_id: deck_id}, resolution) do
    with {:ok, deck_id} <- RelayHelpers.node_id(deck_id, :deck, resolution) do
      deck_id
      |> Catalog.get_deck!(preload?: false)
      |> Catalog.list_deck_question_answers()
      |> then(&{:ok, &1})
    end
  end

  def shared_deck(_parent, %{token: token}, _resolution),
    do: {:ok, Catalog.get_deck_by_share_token(token, preload?: false)}

  def deck_export_text(_parent, %{id: id}, resolution) do
    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      {:ok, id |> Catalog.get_deck!() |> Catalog.export_decklist()}
    end
  end

  def deck_buylist(_parent, %{id: id} = args, resolution) do
    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      {:ok, id |> Catalog.get_deck!() |> Catalog.deck_buylist(deck_buylist_opts(args))}
    end
  end

  def deck_buylist_export(_parent, %{id: id} = args, resolution) do
    format = Map.get(args, :format, "text")

    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      {:ok,
       id |> Catalog.get_deck!() |> Catalog.export_deck_buylist(format, deck_buylist_opts(args))}
    end
  end

  def deck_edhrec(_parent, %{id: id} = args, resolution) do
    opts = [
      exclude_lands: Map.get(args, :exclude_lands, false),
      offset: Map.get(args, :offset, 0),
      commander_name: Map.get(args, :commander_name),
      commander_theme: Map.get(args, :commander_theme)
    ]

    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      case id |> Catalog.get_deck!() |> Catalog.deck_edhrec(opts) do
        {:ok, result} -> {:ok, result}
        {:error, reason} -> {:error, Errors.edhrec_error(reason)}
      end
    end
  end

  def deck_recommander(_parent, %{id: id}, resolution) do
    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      case id |> Catalog.get_deck!() |> Catalog.deck_recommander() do
        {:ok, result} -> {:ok, result}
        {:error, reason} -> {:error, Errors.recommander_error(reason)}
      end
    end
  end

  def deck_combos(_parent, %{id: id}, resolution) do
    with {:ok, id} <- RelayHelpers.node_id(id, :deck, resolution) do
      case id |> Catalog.get_deck!() |> Catalog.deck_combos() do
        {:ok, combos} -> {:ok, combos}
        {:error, reason} -> {:error, Errors.commander_spellbook_error(reason)}
      end
    end
  end

  # Public so mutation resolvers can parse a selector's filters the same way
  # the list/count queries do.
  def collection_filters(args, resolution) do
    with {:ok, filters} <-
           args
           |> Map.get(:filters, %{})
           |> Enum.into([])
           |> put_location_filter_id(resolution),
         {:ok, filters} <- put_card_filter_id(filters, resolution) do
      {:ok, stringify_filter_id(filters, :location_id)}
    end
  end

  defp put_location_filter_id(filters, resolution) do
    case Keyword.fetch(filters, :location_id) do
      {:ok, "unfiled"} -> {:ok, filters}
      _other -> RelayHelpers.put_filter_node_id(filters, :location_id, :location, resolution)
    end
  end

  defp put_card_filter_id(filters, resolution) do
    case Keyword.fetch(filters, :card_id) do
      {:ok, value} ->
        with {:ok, id} <- optional_card_id(value, resolution) do
          {:ok, Keyword.put(filters, :card_id, id)}
        end

      :error ->
        {:ok, filters}
    end
  end

  defp optional_card_id(nil, _resolution), do: {:ok, nil}
  defp optional_card_id("", _resolution), do: {:ok, nil}
  defp optional_card_id(id, resolution), do: card_id(id, resolution)

  defp stringify_filter_id(filters, key) do
    Keyword.update(filters, key, nil, fn
      nil -> nil
      id -> to_string(id)
    end)
  end

  defp card_id(id, resolution) do
    case RelayHelpers.node_id(id, :card, resolution) do
      {:ok, id} -> {:ok, id}
      {:error, _message} when is_binary(id) -> {:ok, id}
      {:error, message} -> {:error, message}
    end
  end

  defp deck_buylist_opts(args) do
    [
      printing_mode: Map.get(args, :printing_mode, "none"),
      include_basic_lands: Map.get(args, :include_basic_lands, false),
      assume_no_owned: Map.get(args, :assume_no_owned, false),
      include_considering: Map.get(args, :include_considering, false)
    ]
  end

  defp location_by_id("unfiled"), do: {:ok, unfiled_location()}
  defp location_by_id(id), do: {:ok, Catalog.get_location_summary!(location_id(id))}

  defp unfiled_location(summaries \\ nil) do
    summary = Catalog.unfiled_location_summary(summaries)

    %{
      id: "unfiled",
      name: "Unfiled",
      kind: "unfiled",
      description: "Cards without an assigned location.",
      cover_printing: nil,
      item_count: summary.item_count,
      total_price_cents: summary.total_price_cents,
      purchase_price_cents: summary.purchase_price_cents
    }
  end

  defp location_id(id) when is_integer(id), do: id

  defp location_id(id) when is_binary(id) do
    case Integer.parse(id) do
      {parsed, ""} -> parsed
      _other -> id
    end
  end
end
